//台詞入れマクロ
//台詞を見つけて、その先頭にmv数字:を入力していく。数字は１づつ増加していきます。
//このマクロではmv命令を使っていますが、46行目を書き換えれば変更出来ます。
$0=input ("台詞入れマクロ 名前を入力してください","芽衣");//台詞を処理するキャラ名
#sn=val(input("台詞のスタートＮＯを入れてください：","0"));	//台詞NO（最初)

$1=$0+"「";//"名前「"
$2=$0+"『";//"名前『"
$3=$0+"（";//"名前（"
//全ての行について処理を実行
while (1) {
	golineend2;
	golinetop2;
	call linefunc;
	golineend2;
	#n=lineno;
	right;
	if (lineno==#n) break;
}
message "終了";
enabledraw;
endmacro;

linefunc:
//命令文の場合は飛ばす
$$0=gettext (x,y,x+strlen($0)+2,y);
if ($$0!=$1 && $$0!=$2 && $$0!=$3) {
	return;
}
//#counter=0;
#i=0;
while (#i<strlen($0)/2) {
	delete;
	#i=#i+1;
}

//そのキャラの台詞
if (#sn>=10000) $num=str(#sn); else {
	$num="0";
	if (#sn<1000) $num=$num+"0";
	if (#sn<100) $num=$num+"0";
	if (#sn<10) $num=$num+"0";
	$num=$num+str(#sn);
}
#sn=#sn+1;
insert "dv"+$num+":\n"+$0;
golinetop2;
selectline;
golineend2;
question "続行しますか？";
if( result == no ) {
	escape;
	endmacro;
}
escape;
return;

