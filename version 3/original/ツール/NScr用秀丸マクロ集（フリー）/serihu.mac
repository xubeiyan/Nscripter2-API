//台本作成補助台詞ナンバリングマクロ
//名前「　名前『　名前（　で始まる行を台詞とみなし、
//[数字:【名前】「台詞」]という形式に変換。
//数字は５桁になります。頭の桁をキャラクタ識別に使うといいでしょう。
//[]でくくってあるのは、あとでWordで検索しやすいようにするためです。
//Wordの置換機能で正規表現を使い、"\[*\]"を検索して、太字とか大きなフォントとかにいじってください。

$0=input ("名前を入力してください","絵梨奈");//台詞を処理するキャラ名
#sn=val(input("台詞のスタートＮＯを入れてください：","0"));	//台詞NO（最初)

gofiletop;
$1=$0+"「";//"名前「"
$2=$0+"『";//"名前『"
$3=$0+"（";//"名前（"
//全ての行について処理を実行
while (1) {
	golineend2;
	golinetop2;
	call linefunc;
	golineend2;
	#n=lineno;
	right;
	if (lineno==#n) break;
}
message "終了";
endmacro;

linefunc:
//命令文の場合は飛ばす
$$0=gettext (x,y,x+strlen($0)+2,y);
if ($$0!=$1 && $$0!=$2 && $$0!=$3) {
	return;
}
#i=0;
while (#i<strlen($0)/2) {
	delete;
	#i=#i+1;
}

//そのキャラの台詞
if (#sn>=10000) $num=str(#sn); else {
	$num="0";
	if (#sn<1000) $num=$num+"0";
	if (#sn<100) $num=$num+"0";
	if (#sn<10) $num=$num+"0";
	$num=$num+str(#sn);
}
#sn=#sn+1;
insert "["+$num+":【"+$0+"】";
golineend2;
insert "]";
return;

