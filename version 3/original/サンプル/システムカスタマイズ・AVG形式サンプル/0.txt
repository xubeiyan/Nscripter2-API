;ＮＳｃｒでＡＶＧ形式のものを作るときのシステムカスタマイズの
;最低限のテンプレートです。

*text_lb
;ここでは最低限のことしかしていませんが、
;ボタンを設定して処理を組み込むことも出来ます。
;カーソルはスプライトで実装してください。

btndef clear
*tbloop
textbtnwait %0
if %0=0 goto *text_end
if %0=-3 goto *text_end		;普通のクリックとホイール↓入力
if %0=-1 gosub *rclk:goto *text_lb	;右クリック
if %0=-2 systemcall lookback:goto *tbloop	;ホイール上入力
goto *tbloop

*text_end
texec
saveon
return






*customsel
skipoff
*csel_st
btndef clear
getcselnum %0
getcursorpos %1,%2
cselbtn 0,500,%1,%2
add %2,21
cselbtn 1,501,%1,%2
if %0>2 add %2,21:cselbtn 2,502,%1,%2
if %0>3 add %2,21:cselbtn 3,503,%1,%2
if %0>4 add %2,21:cselbtn 4,504,%1,%2

*csel_loop
selectbtnwait %0
if %0=-2 systemcall lookback:goto *csel_loop
if %0=-1 gosub *rclk:goto *csel_st
if %0>=500 & %0<=504 saveon:sub %0,500:cselgoto %0
goto *csel_loop







*rclk
;右クリック処理
systemcall rmenu
;ここでは単にメニューを読んでますが、
;ここにボタン処理でメニューを組むことが出来ます。
;systemcall文やsavegame文などをうまく使ってください。
return









*define
rmenu "ウィンドウを消す",WINDOWERASE,"セーブ",SAVE,"ロード",LOAD,"タイトルに戻る",RESET
menusetwindow 18,18,1,1,1,1,#885522
caption "ＡＶＧサンプル"
humanz 100
windowback
textgosub *text_lb
linepage
usewheel
game

*start
erasetextwindow 0	;これでテキストウィンドウがでっぱなしになります。

bg #aa8844,10,1000
click
setwindow 30,380,25,5,20,20,0,1,0,1,1,#BB8866,20,370,620,460
goto *game_start


*game_start
ＡＶＧスタイルのサンプルです。
これを参考にして色々組んでください。
選択肢も表示されます。

csel "選択肢１",*s1,"選択肢２",*s2,"選択肢３",*s3

*s1
選択肢１を選択しました。\
end


*s2
選択肢２を選択しました。\
end

*s3
選択肢３を選択しました。\
end